#include "p18f4550.inc"
; ; ;
;   TRANSMIT STATUS AND CONTROL REG
;	CSRC	- ASYNC MODE. NO CLOCK SOURCE
;	TX9	- SELECT 8 BIT TX
;	TXEN	- TRANSMIT EN
;	SYNC	- ASYNC
;	SENDB	- SYNC BREAK TRANSMIMSSION COMPLETED
;	BRGH	- HIGH SPEED
;	TRMT	- TSR FULL
;	TX9D	- 
; ; ;
UART_TXSTA	EQU	b'00100100'
; ; ;
;   RCSTA RECEIVE STATUS AND CONTROL REG
;	SPEN	- SERIAL PORT EN
;	RX9	- 8 BIT MODE
;	SREN	- ASYNC
;	CREN	- EN RX
;	ADDEN	- ASYNC
;	FERR	- NO FARMING ERROR
;	OERR	- NO OVERRUN ERROR
;	RX9D	- NULL
; ; ;
UART_RCSTA	EQU	b'10010000'
; ; ;
;   BAUDCON BAUD RATE CONTROL REG
;	ABDOVF	- NOT USED
;	RCIDL	- RECEIVE OPERATION IS IDLE
;	RXDTP	- RX IS NOT INVERTED
;	TXCKP	- TX IS NOT INVERTED
;	BRG16	- 8 BIT BAUDRATE GEN
;	NULL
;	WUE	- WAKE-UP EUSART RX INTERRUPT FALLING EDGE
;	ABDEN	- BAUD RATE MEASUREMENT DISABLED
; ; ;
UART_BAUDCON	EQU	b'01000010'
; ; ;
;   SPBRG   BAUD RATE GENERATOR REGISTER WORD
;	SYNC	= 0
;	BRG16	= 0
;	BRGH	= 1
;	BAUDRATE = FOSC / ( 16 ( N+1 ) )
;	    N IS SPBRG VALUE
;	1 = ( BAUD )( 16N+16 ) /( FOSC )
;	( FOSC / ( 16( BAUD ) ) ) - 1 = N
; ; ;
UART_FOSC	EQU	.48000000;Hz
UART_BAUD	EQU	.115200;Baud
UART_SPBRG	EQU	((UART_FOSC/(.16*UART_BAUD))-1) & 0xFF ;.25
UART_RETURN_REG	EQU	0x00

;   LET THE LINKER ALLOCATE DATA
UART_PTR	UDATA
UART_TX			res	2
UART_RX			res	2
UART_TX_COUNTER		res	1
UART_TX_STATUS		res	1
UART_RX_COUNTER		res	1
UART_RX_STATUS		res	1
		

UART_CONFIG CODE
    ; ;
    ;	Variables globales
    ; ;
    GLOBAL UART_TX_COUNTER
    GLOBAL UART_TX_STATUS
    GLOBAL UART_TX
    GLOBAL UART_RX_COUNTER
    GLOBAL UART_RX_STATUS
    GLOBAL UART_RX
    ; ;
    ;	Funciones globales
    ; ;
    GLOBAL UART_INIT
    GLOBAL UART_START
    GLOBAL UART_STOP
    GLOBAL UART_TRANSMIT
    GLOBAL UART_CANCEL_TRANSMIT
    GLOBAL UART_RECEIVE
    GLOBAL UART_CANCEL_RECEIVE
UART_INIT:
    ;	;
    ;	CONFIGURAR REGISTROS LOCALES
    ;	;
    BANKSEL UART_TX
    ; COUNTER = STATUS = 0x00
    CLRF    UART_TX_COUNTER,1
    CLRF    UART_RX_COUNTER,1
    CLRF    UART_TX_STATUS,1
    CLRF    UART_RX_STATUS,1
    ; PTR = NULL
    CLRF    UART_TX,1
    CLRF    UART_TX+.1,1
    CLRF    UART_RX,1
    CLRF    UART_RX+.1,1
    ;	;
    ;	DESACTIVAR LAS INTERRUPCIONES UART
    ;	;
    BCF	    PIE1,RCIE,0
    BCF	    PIE1,TXIE,0
    ;	;
    ;	LIMPIAR LAS INTERRUPCIONES PENDIENTES
    ;	;
    BCF	    PIR1,RCIF,0
    BCF	    PIR1,TXIF,0
    ; ;
    ;	CONFIGURAR PINES UART
    ; ;
    BCF	    PORTC,RC7,0
    BCF	    PORTC,RC6,0
    BCF	    LATC,RC7,0
    BCF	    LATC,RC6,0
    BSF	    TRISC,RC7,0
    BSF	    TRISC,RC6,0
    ; ;
    ;	CARGAR LAS CONFIGURACIONES EN LOS REGISTROS
    ; ;
    MOVLW   UART_TXSTA
    MOVWF   TXSTA,0
    MOVLW   UART_RCSTA
    MOVWF   RCSTA,0
    MOVLW   UART_BAUDCON
    MOVWF   BAUDCON,0
    MOVLW   (UART_SPBRG&0xFF)
    MOVWF   SPBRG,0
    ;	;
    ;	CONFIGURAR LAS INTERRUPCIONES UART
    ;	;
    BSF	    IPR1,RCIP,0	;   RX INT AS HIGH LEVEL INT
    BSF	    IPR1,TXIP,0	;   TX INT AS HIGH LEVEL INT
    BSF	    RCSTA,SPEN,0;   ACTIVAR EL PUERTO SERIAL
    RETURN
UART_START:
    BSF	    RCSTA,SPEN,0    ;	ACTIVAR EL PUERTO SERIAL
    RETURN
UART_STOP:
    BCF	    RCSTA,SPEN,0    ;	DESACTIVAR EL PUERTO SERIAL
    RETURN
;   ;	;   ;	;   ;	;   ;	;   ;	;   ;	;   ;
;   TRANSMITIR EL PAQUETE DEFINIDO
;   ;	;   ;	;   ;	;   ;	;   ;	;   ;	;   ;
UART_TRANSMIT:
    BANKSEL UART_TX
    ;	VERIFICAR SI TX PENDIENTE
    BTFSC   UART_TX_STATUS,0,1
    BRA	    UART_TRANSMIT_FAIL;	ERROR! TRANSMISION PENDIENTE
    ;	CARGAR EL NUEVO PUNTERO AL REGISTRO
    MOVF    0x00,W,0
    MOVWF   UART_TX,1
    MOVF    0x01,W,0
    MOVWF   UART_TX+.1,1
    ;	CARGAR EL CONTADOR AL REGISTRO
    MOVF    0x02,0
    MOVWF   UART_TX_COUNTER,1
    CLRF    0x00,0
    ;	MARCAR EL INICIO DE UNA NUEVA TRANSMISION
    BSF	    UART_TX_STATUS,0,1
    ;	TRANSMITIR
    BSF	    PIE1,TXIE,0
    CLRF    0x00,0
    RETURN
UART_TRANSMIT_FAIL:
    MOVF   UART_TX_STATUS,W,1
    MOVWF   0x00,0
    RETURN
UART_CANCEL_TRANSMIT:
    BCF	    PIE1,TXIE,0	;   DESACTIVAR LAS INTERRUPCIONES TX
    BANKSEL UART_TX
    MOVLW   0x02
    MOVWF   UART_TX_STATUS,1;	MARCAR COMO CANCELADO
    MOVWF   0x00,0
    RETURN
UART_RECEIVE:
    BANKSEL UART_TX
    ;	VERIFICAR SI NO HAY RECEPCIONES PENDIENTES
    BTFSC   UART_RX_STATUS,0,1
    BRA	    UART_RECEIVE_FAIL;	ERROR! RX PENDIENTE
    ;	CARGAR APUNTADOR Y CONTADOR A REGISTRO
    MOVF    0x00,W,0
    MOVWF   UART_RX,1
    MOVF    0x01,W,0
    MOVWF   UART_RX+.1,1
    MOVF    0x02,W,0
    MOVWF   UART_RX_COUNTER,1
    ;	MARCAR NUEVA RECEPCION
    BSF	    UART_RX_STATUS,0,1
    ;	COMENZAR RECEPCION
    BSF	    PIE1,RCIE,0
    CLRF    0x00,0
    RETURN
UART_RECEIVE_FAIL:
    MOVF    UART_RX_STATUS,W,1
    MOVWF   0x00,0
    RETURN
UART_CANCEL_RECEIVE:
    BCF	    PIE1,RCIE,0	;   DESACTIVAR LAS INTERRUPCIONES TX
    BANKSEL UART_TX
    MOVLW   0x02
    MOVWF   UART_RX_STATUS,1;	MARCAR COMO CANCELADO
    MOVWF   0x00,0
    RETURN
    END